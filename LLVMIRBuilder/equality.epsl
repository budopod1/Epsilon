#import eewriter;

Val#do [Val:outer_a] equals [Val:outer_b] deep: [Bool:deep] invert: [Bool:invert] {
    assert type_ equality [outer_a], [outer_b];
    Type_:type_ = outer_a.type_;

    if ([type_].is_numeric) {
        if (invert) {
            return [outer_a] ne [outer_b];
        } else {
            return [outer_a] eq [outer_b];
        };
    } elif ([type_.name] equals ["Null"]) {
        return c [!invert];
    } elif ([type_.name] equals ["Internal"]) {
        return c [invert];
    };

    Str:name = ["equals"].clone;
    if (deep) {
        [name].extend["_deep"];
    };
    if (invert) {
        [name].extend["_invert"];
    };
if (specialized fn [Bool_] [name] [[Val] [outer_a, outer_b]]) {
    Val:a = func arg [0];
    Val:b = func arg [1];

    if ([type_.name] equals ["Array"]) {
        Val:a_len = load [[a].length];
        Val:b_len = load [[b].length];
        if_ [[a_len] ne [b_len]];
            ret [c [invert]];
        end block;

        Val:a_content = load [[a].content];
        Val:b_content = load [[b].content];
        Val:i = var [c L_ [0]];
    Label:check_label = new label:;
        Val:cur_i = load [i];
        while_ [[cur_i] lt [b_len]] from [check_label];
            Val:a_elem = [a_content].idx[cur_i];
            Val:b_elem = [b_content].idx[cur_i];

            Val?:exit = null;
            if (deep) {
                exit = do [a_elem] equals [b_elem] deep: [true] invert: [true];
            } else {
                exit = [a_elem] ne [b_elem];
            };
            if_ [[exit].unwrap];
                ret [c [invert]];
            end block;

            [i] = [[cur_i] + [c L_ [1]]];
        end block;

        ret [c [!invert]];
    } elif ([type_.name] equals ["Optional"]) {
        Val:a_is_null = [a] == null_;
        Val:b_is_null = [b] == null_;
        if_ [a_is_null];
            if (invert) {
                ret [NOT [b_is_null]];
            } else {
                ret [b_is_null];
            };
        else_;
            if_ [b_is_null];
                ret [c [invert]];
            else_;
                Type_:nonnull_type_ = unwrap nullability [type_];
                Val:nonnull_a = cast [a] to [nonnull_type_];
                Val:nonnull_b = cast [b] to [nonnull_type_];
                ret [do [nonnull_a] equals [nonnull_b] deep: [deep] invert: [invert]];
            end block;
        end block;
        mark as unreachable;
    } elif ([type_].is_poly) {
        // since we don't yet support true value-based equality between poly
        // types_, we have to revert to checking for object identity
        if (invert) {
            ret [[a] ne [b]];
        } else {
            ret [[a] eq [b]];
        };
    } else {
        Struct_?:maybe_struct = [type_].get_struct;

        if (!maybe_struct) {
            abort "Don't know how to check equality of type_ {}" % [type_].stringify;
        };

        Struct_:struct_ = [maybe_struct].unwrap;

        for (L:i enumerating struct_.fields) {
            Val:a_elem = load [[a].[i]];
            Val:b_elem = load [[b].[i]];
            Val?:exit = null;
            if (deep) {
                exit = do [a_elem] equals [b_elem] deep: [true] invert: [true];
            } else {
                exit = [a_elem] ne [b_elem];
            };
            if_ [[exit].unwrap];
                ret [c [invert]];
            end block;
        };

        ret [c [!invert]];
    };
end fn;
};

    return func ret;
}

Val#[Val:outer_val] equals const str [Str:target] {
    Val:outer_const_str = const str [target];
    private accessibility;
if (fn [Bool_] ["str_equals_const_str"] [[Type_] [Str_, L_, Pointer [Byte_]]]
    [[Val] [outer_val, c L_ [[target].len], outer_const_str]]) {
    Val:val = func arg [0];
    Val:target_len = func arg [1];
    Val:const_str = func arg [2];

    if_ [[[val].length] ne [target_len]];
        ret [c [false]];
    end block;

    extern fn [Z_] ["epsl_memcmp"] [[Type_] [Pointer [Byte_], Pointer [Byte_], L_]]
        [[Val] [[val].content, const_str, target_len]];

    ret [[func ret] eq [c [(Z)0]]];

end fn;
};

    return func ret;
}
